#!/usr/bin/perl -w
#
#  Simple script to request a lot of URLs and compare the content
# against an expected string.
#
#  This is used to test Steve's personal server, but might be useful
# for others.
#
# Steve
# --
#



use strict;
use warnings;

use LWP::UserAgent;



#
#  Failure count + Test count
#
my $fail  = 0;
my $count = 0;


while ( my $line = <DATA> )
{

    #
    #  Skip empty lines and comments.
    #
    chomp($line);
    next if ( !length($line) || $line =~ /^#/ );

    #
    #  The URL + content
    #
    if ( $line =~ /([^ \t]+)([ \t]+)"([^"]+)"$/ )
    {
        my $url     = $1;
        my $content = $3;
        my $text    = getContent($url);

        if ( defined $text )
        {
            if ( $text !~ /\Q$content\E/i )
            {
                $fail += 1;
                print "ERR $url doesn't contain '$content'\n";
            }
        }
        else
        {
            $fail += 1;
            print "ERR - Failed to fetch $url\n";
        }

        $count += 1;
    }
}

my $ok = $count - $fail;

print "Tests completed: [$ok/$count]\n";
print "Failed tests   : $fail\n" if ($fail);




sub getContent
{
    my ($url) = (@_);

    #
    #  Create a user-agent.
    #
    my $agent = LWP::UserAgent->new( max_redirect => 1 );

    #
    #  Get the response
    #
    my $response = $agent->get($url);
    return undef if ( !defined($response) );

    #
    # Get the text
    #
    if ( $response->is_success )
    {
        return ( $response->decoded_content() );
    }
    else
    {
        return undef;
    }

}




__DATA__

#
#  Steve.org.uk
#
http://steve.org.uk/  "Steve Kemp"
http://www.steve.org.uk/  "Steve Kemp"


#
#  Repository
#
http://itag.repository.steve.org.uk                        "ChangeLog"
http://repository.steve.org.uk/cgi-bin/hgwebdir.cgi/itag/  "ChangeLog"


#
#  Stolen-SOuls
#
http://stolen-souls.com/               "Amateur"
http://www.stolen-souls.com/           "Amateur"
http://www.stolen-souls.com/links/     "modelmayhem"

#
#  TODO
#
http://todo.steve.org.uk/            "/account/create"

#
#  Static
#
http://static.steve.org.uk/        "javascript libraries"


#
#  IPv4 & IPv6
#
http://ipv4.steve.org.uk/       "ipv4 test host"
http://ipv6.steve.org.uk/       "ipv6 test host"
